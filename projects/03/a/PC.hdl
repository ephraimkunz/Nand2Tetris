// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Register(in=regIn, load=true, out=out, out=outLoopback);
    Inc16(in=outLoopback, out=outLoopbackInc);

    // Control - priority is backwards left to right from top to bottom of if statement.
    Mux16(a=loadedOut, b=false, sel=reset, out=regIn);
    Mux16(a=incOut, b=in, sel=load, out=loadedOut);
    Mux16(a=outLoopback, b=outLoopbackInc, sel=inc, out=incOut);
}
