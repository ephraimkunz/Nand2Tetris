// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    RAM64(in=in, load=chip0Enable, address=address[0..5], out=out0);
    RAM64(in=in, load=chip1Enable, address=address[0..5], out=out1);
    RAM64(in=in, load=chip2Enable, address=address[0..5], out=out2);
    RAM64(in=in, load=chip3Enable, address=address[0..5], out=out3);
    RAM64(in=in, load=chip4Enable, address=address[0..5], out=out4);
    RAM64(in=in, load=chip5Enable, address=address[0..5], out=out5);
    RAM64(in=in, load=chip6Enable, address=address[0..5], out=out6);
    RAM64(in=in, load=chip7Enable, address=address[0..5], out=out7);

    DMux8Way(in=load, sel=address[6..8], a=chip0Enable, b=chip1Enable, c=chip2Enable, d=chip3Enable, e=chip4Enable, f=chip5Enable, g=chip6Enable, h=chip7Enable);
    Mux8Way16(a=out0, b=out1, c=out2, d=out3, e=out4, f=out5, g=out6, h=out7, sel=address[6..8], out=out);
}